---
page_title: "Provider: resourcely"
description: |-
  Manage Resourcely blueprints, guardrails, global values, and more through Terraform.
---

# RESOURCELY Provider

The Resourcely provider is used to manage Resourcely blueprint,
guardrails, global values, and more through Terraform. The provider
needs to be configured with the proper credentials before it can be
used.

## Example Usage

Configure the Resourcely provider.

```terraform
# Pin the source and version of the provider
terraform {
  required_providers {
    resourcely = {
      source  = "Resourcely-Inc/resourcely"
      version = "~> 1.0"
    }
  }
}

# No provider configuration is required. See `Authentication and
# Configuration` below to learn how to provide your Resourcley API
# credentials.
provider "resourcely" {
}
```

Create a guardrail.

```terraform
resource "resourcely_guardrail" "s3_bucket_naming_convention" {
  name        = "S3 Bucket Naming Convention"
  description = "Ensures that all S3 Buckets comply with our standardized naming convention, promoting consistency and ease of identification across our AWS environments."

  cloud_provider = "PROVIDER_AMAZON"
  category       = "GUARDRAIL_BEST_PRACTICES"

  content = <<-EOT
    GUARDRAIL "S3 Bucket Naming Convention"
      WHEN aws_s3_bucket
        REQUIRE bucket STARTS WITH "mycompany-"
  EOT
}
```

Create a blueprint.

```terraform
resource "resourcely_blueprint" "private_s3_bucket" {
  name        = "Private S3 Bucket"
  description = "Creates a private, S3 bucket with configuration versioning."

  cloud_provider = "PROVIDER_AMAZON"
  categories     = ["BLUEPRINT_BLOB_STORAGE"]

  is_published = true

  content = <<-EOT
    ---
    constants:
      __name: "{{ bucket }}_{{ __guid }}"
    ---
    resource "aws_s3_" "{{ __name }}" {
      bucket = {{ bucket }}
    }

    resource "aws_s3_bucket_public_access_block" "{{ __name }}" {
      bucket = aws_s3_bucket.{{ __name }}.id

      block_public_acls       = true
      block_public_policy     = true
      ignore_public_acls      = true
      restrict_public_buckets = true
    }

    resource "aws_s3_bucket_ownership_controls" "{{ __name }}" {
      bucket = aws_s3_bucket.{{ __name }}.id

      rule {
        object_ownership = "BucketOwnerEnforced"
      }
    }
  EOT
}
```

## Authentication and Configuration

The provider requires a Resourcely API token to authenticate. It
expects the token to be provided through the `RESOURCELY_AUTH_TOKEN`
environment variable. Configure your Terraform runner to supply this
variable.

You can generate the token from the [Resourcely Settings
page](https://portal.resourcely.io/settings/generate-api-token). Choose
the "Terraform Provider" role.

You can also supply the token directly in the provider block. Ensure
that token is securely stored in a secret management system. Do not
hardcode it in the provider block.

```terraform
provider "resourcely" {
  auth_token = var.resourcely_auth_token
}
```

If your organization uses multiple tenants within Resourcely, you can
configure the `allowed_tenants` in the provider block to prevent
accidently mixing API keys between tenants.

```terraform
provider "resourcely" {
  allowed_tenants = ["MyCompany"]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `allowed_tenants` (List of String) List of allowed tenant names (case-insensitive) to prevent accidently applying a configuration to the wrong one.
- `auth_token` (String, Sensitive) Authorization token for Resourcely API.
- `host` (String) URI for Resourcely API. Defaults to 'https://api.resourcely.io'.
