---
page_title: "resourcely_global_value Resource - terraform-provider-resourcely"
subcategory: ""
---

# resourcely_global_value (Resource)

A [global value](https://docs.resourcely.io/concepts/other-features-and-settings/global-values) allows admins to define custom drop-downs for customizing Terraform infrastructure resource properties before they are provisioned.  They are useful for providing access to lists of relatively static values like VPC IDs, allowed regions, department or team names, etc.

This global value API does not support deletion. Deleting the Terraform resource will remove the resource from the Terraform state file, but will not actually delete the global value entity. Set `is_deprecated = true` to tell Resourcely that this global value should no longer be used by new blueprints or guardrails.

## Example Usage

Create a global value containing all the departments within the company.

```terraform
resource "resourcely_global_value" "departments" {
  name        = "Departments"
  key         = "departments"
  description = "All departments within MyCompany"

  type = "PRESET_VALUE_TEXT"
  options = [
    {
      key   = "product",
      label = "Product",
      value = "product",
    },
    {
      key   = "it",
      label = "IT",
      value = "IT",
    },
    {
      key   = "marketing",
      label = "Marketing",
      value = "market",
    },
    {
      key   = "engingeering",
      label = "Engineering",
      value = "eng",
    },
  ]
}
```

Create a global value listing the AMI attributes for three Linux
distributions. Then create a blueprint that creates an instance,
querying for the AMI using the attributes of the selected
distribution.

```terraform
resource "resourcely_global_value" "amis" {
  name        = "EC2 AMI"
  key         = "amis"
  description = "AMI selectors."

  type = "PRESET_VALUE_OBJECT"
  options = [
    {
      key   = "ubuntu",
      label = "Ubuntu",
      value = {
        name      = "ubuntu/images/hvm-ssd/ubuntu-jammy-22.04-amd64-server-*",
        virt_type = "hvm",
        owner     = "099720109477",
      }
    },
    {
      key   = "centos",
      label = "CentOS",
      value = {
        name      = "CentOS Stream 9 x84_64 *",
        virt_type = "hvm",
        owner     = "125523088429",
      },
    },
    {
      key   = "debian",
      label = "Debian",
      value = {
        name      = "debian-12-amd64-*",
        virt_type = "hvm",
        owner     = "136693071363",
      },
    },
  ]
}

resource "resourcely_blueprint" "simple_instance" {
  name        = "A simple EC2 Instance"
  description = "Creates a simple EC2 instance using one of three preselected AMIs."

  cloud_provider = "PROVIDER_AMAZON"
  categories     = ["BLUEPRINT_COMPUTE"]

  is_published = true

  content = <<-EOT
    ---
    constants:
      __name: "{{ bucket }}_{{ __guid }}"
    variables:
      ami:
        description: The Linux distribution to run on this instance.
        global_value: amis
    ---
    data "aws_ami" "{{ __name }}" {
      most_recent = true

      filter {
        name   = "name"
        values = [{{ ami.name }}]
      }

      filter {
        name   = "virtualization-type"
        values = [{{ ami.virt_type }}]
      }

      owners = [{{ ami.owner }}]
    }

    resource "aws_instance" "{{ __name }}" {
      ami           = data.aws_ami.{{ __name }}.id
      instance_type = "t3.micro"

      tags = {
        Name = "HelloWorld"
      }
    }
  EOT
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `key` (String) An immutable identifier used to reference this global value in blueprints or guardrails. Must start with a lowercase letter in `a-z` and include only characters in `a-z0-9_`.
- `name` (String) The name of the global value.
- `options` (Attributes List) The list of value options for this global value. (see [below for nested schema](#nestedatt--options))
- `type` (String) The type of options in the global value. Can be one of `PRESET_VALUE_TEXT`, `PRESET_VALUE_NUMBER`, `PRESET_VALUE_LIST`, `PRESET_VALUE_OBJECT`

### Optional

- `description` (String) A description of the purpose of the global value.
- `is_deprecated` (Boolean) Set to true if the global value should not be used in new blueprints or guardrails.

### Read-Only

- `id` (String) UUID for the current version of the global value.
- `series_id` (String) UUID for the global value.
- `version` (Number) Incrementing version number for the current version of the global value.

<a id="nestedatt--options"></a>
### Nested Schema for `options`

Required:

- `key` (String) An immutable identifier for ths option. Must start with a lowercase letter in `a-z` and include only characters in `a-z0-9_`.
- `label` (String) A unique display name.
- `value` (String) A JSON encoding of the option's value. This value must match the declared type of the global value. Example: `value = jsonencode("a")` Example: `value = jsonencode(["a", "b"])`

Optional:

- `description` (String) A description of this option's meaning.

## Import

A global value can be imported using its series_id.

```shell
terraform import resourcely_global_value.example 00000000-00000000-00000000-00000000
```
