---
page_title: "resourcely_context_question Resource - terraform-provider-resourcely"
subcategory: ""
---

# resourcely_context_question (Resource)

A [context question](https://docs.resourcely.io/concepts/other-features-and-settings/global-context-and-values) is used to gather data from developers before provisioning a resource. They are designed to gather and store insightful data related to the resource.

Some examples include:

- What type of data will be stored in this infrastructure?
- What application is this infrastructure associated with?
- What is the email address the person/team responsible for this infrastructure?

Three types of context questions are supported:

- Text
- Single Choice
- Multiple Choice

## Example Usage

Create a simple,  multi-select context question.

```terraform
resource "resourcely_context_question" "data_classification" {
  label  = "classification"
  prompt = "What kind of data is stored by this infrastructure?"

  qtype = "QTYPE_MULTI_SELECT"
  answer_choices = [
    { label = "financial" },
    { label = "pii" },
    { label = "proprietary" },
    { label = "public" },
  ]

  blueprint_categories = ["BLUEPRINT_BLOB_STORAGE", "BLUEPRINT_DATABASE"]

  priority = 1
}
```


Context questions with "text" type can restrict the allowed answer formats. For example, create a context question that asks for an email address.

```terraform
resource "resourcely_context_question" "owner" {
  label  = "owner"
  prompt = "What is the email address of the team or person responsible for this infrastructure?"

  qtype         = "QTYPE_TEXT"
  answer_format = "ANSWER_EMAIL"

  blueprint_categories = [
    "BLUEPRINT_COMPUTE",
    "BLUEPRINT_DATABASE",
    "BLUEPRINT_NETWORKING",
    "BLUEPRINT_SERVERLESS_COMPUTE"
  ]

  priority = 0
}
```

Or, create a context question that asks for a project code comprising 6 hex digits.

```terraform
resource "resourcely_context_question" "project_code" {
  label  = "project_code"
  prompt = "What is the project code that this infrastructure should be  associated with?"

  qtype         = "QTYPE_TEXT"
  answer_format = "ANSWER_REGEX"
  regex_pattern = "^[A-Z0-9]{6}$"

  blueprint_categories = [
    "BLUEPRINT_COMPUTE",
    "BLUEPRINT_DATABASE",
    "BLUEPRINT_NETWORKING",
    "BLUEPRINT_SERVERLESS_COMPUTE"
  ]

  priority = 2
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `label` (String) A key used to reference the context question in blueprints and guardrails. Must be unique within your Resourcley tenant.
- `prompt` (String) The question that Resourcely with ask your developers.
- `qtype` (String) The type of the question. Must be one of `QTYPE_TEXT`, `QTYPE_SINGLE_SELECT`, or `QTYPE_MULTI_SELECT`
- `scope` (String)

### Optional

- `answer_choices` (Attributes Set) The answer choices from which the developer can select. Applicable only when `qtype` is `QTYPE_SINGLE_SELECT` or `QTYPE_MULTI_SELECT`. (see [below for nested schema](#nestedatt--answer_choices))
- `answer_format` (String) A format validation for acceptable answers to the context question. Applicable only when `qtype` is `QTYPE_TEXT` . Must be one of `ANSWER_TEXT`, `ANSWER_NUMBER`, `ANSWER_EMAIL`, or `ANSWER_REGEX`. If `ANSWER_REGEX`, must also specify the `regex_pattern` property.
- `blueprint_categories` (Set of String) The blueprint categories to which this context question applies. This question will be asked whenever a developer uses a blueprint in these categories.
- `priority` (Number) The priority of this question, relative to others. 0=high, 1=medium, 2=low
- `regex_pattern` (String) A regex validation for the acceptable answers to the context question. Applicable only when both `qtype` is `QTYPE_TEXT` and `answer_format` is `ANSWER_REGEX`.

### Read-Only

- `id` (String) UUID for the current version of the context question.
- `series_id` (String) UUID for the context question.
- `version` (Number) Incrementing version number for the current version of this context question.

<a id="nestedatt--answer_choices"></a>
### Nested Schema for `answer_choices`

Required:

- `label` (String) The value for the answer choice.

## Import

A blueprint can be imported using its series_id.

```shell
terraform import resourcely_context_question.example 00000000-00000000-00000000-00000000
```
