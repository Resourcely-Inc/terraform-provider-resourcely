---
page_title: "resourcely_guardrail Resource - terraform-provider-resourcely"
subcategory: ""
---

# resourcely_guardrail (Resource)

A guardrail governs how cloud resources can be created and altered, preventing infrastructure misconfiguration. Before infrastructure is provisioned, Resourcely examines the changes being made and prevents a merge if any guardrail requirements are violated.

Some examples of guardrails include:

- Require approval for making a public S3 bucket
- Restrict the allowed compute instance types or images

Guardrails are specified using the [Really policy language](https://docs.resourcely.io/build/setting-up-guardrails/authoring-your-own-guardrails).

## Example Usage

Create a guardrail using the [Really policy langauge](https://docs.resourcely.io/build/setting-up-guardrails/authoring-your-own-guardrails).

```terraform
resource "resourcely_guardrail" "s3_bucket_naming_convention" {
  name        = "S3 Bucket Naming Convention"
  description = "Ensures that all S3 Buckets comply with our standardized naming convention, promoting consistency and ease of identification across our AWS environments."

  cloud_provider = "PROVIDER_AMAZON"
  category       = "GUARDRAIL_BEST_PRACTICES"

  content = <<-EOT
    GUARDRAIL "S3 Bucket Naming Convention"
      WHEN aws_s3_bucket
        REQUIRE bucket STARTS WITH "mycompany-"
  EOT
}
```

This resource also supports creating a guardrail from a Resourcely
Guardrail Template. Instead of specifying the `content`, specify the
`guardrail_template_series_id` and `guardrail_template_inputs`
instead.

```terraform
resource "resourcely_guardrail" "s3_bucket_naming_convention_from_template" {
  name        = "S3 Bucket Naming Convention"
  description = "Ensures that all S3 Buckets comply with our standardized naming convention, promoting consistency and ease of identification across our AWS environments."

  cloud_provider = "PROVIDER_AMAZON"
  category       = "GUARDRAIL_BEST_PRACTICES"

  guardrail_template_series_id = "4909a93c-b248-4e5a-bff5-7cc101702351"
  guardrail_template_inputs = jsonencode({
    prefix   = "mycompany-"
    approver = "default"
  })
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `category` (String) The category to assign to this guardrail. Can be one of `GUARDRAIL_ACCESS_CONTROL`, `GUARDRAIL_BEST_PRACTICES`, `GUARDRAIL_CHANGE_MANAGEMENT`, `GUARDRAIL_COST_EFFICIENCY`, `GUARDRAIL_ENCRYPTION`, `GUARDRAIL_GLOBALIZATION`, `GUARDRAIL_IAM`, `GUARDRAIL_LOGGING`, `GUARDRAIL_MODULE_INPUTS`, `GUARDRAIL_PRIVACY_COMPLIANCE`, `GUARDRAIL_RELIABILITY`, `GUARDRAIL_STORAGE_AND_SCALE`.
- `cloud_provider` (String) The cloud provider that this guardrail targets. Can be one of `PROVIDER_AMAZON`, `PROVIDER_AZURE`, `PROVIDER_CONDUCTORONE`, `PROVIDER_DATABRICKS`, `PROVIDER_DATADOG`, `PROVIDER_GITHUB`, `PROVIDER_GITLAB`, `PROVIDER_GOOGLE`, `PROVIDER_HYPERV`, `PROVIDER_IBM`,`PROVIDER_JUMPCLOUD`, `PROVIDER_KUBERNETES`, `PROVIDER_OKTA`, `PROVIDER_ORACLE`, `PROVIDER_RESOURCELY`, `PROVIDER_SNOWFLAKE`, `PROVIDER_SPACELIFT`, `PROVIDER_VMWARE`, `PROVIDER_OTHER`.
- `name` (String) The name of the guardrail.

### Optional

- `content` (String) The guardrail policy written in the [Really policy language](https://docs.resourcely.io/build/setting-up-guardrails/authoring-your-own-guardrails). Must specify exactly one of `content` or `guardrail_template_series_id`.
- `description` (String) A description of the guardrail's purpose or policy.
- `guardrail_template_inputs` (String) A JSON encoding of values for the guardrail template inputs. Required if `guardrail_template_series_id` is used. Example: `guardrail_template_inputs = jsonencode({inputOne = "value one"})`
- `guardrail_template_series_id` (String) The series id of the guardrail template used to render the policy. Must specify exactly one of `guardrail_template_series_id` or `content`.
- `scope` (String)
- `state` (String) The [state](https://docs.resourcely.io/build/setting-up-guardrails/releasing-guardrails#guardrail-status) of the guardrail. Can be one of `GUARDRAIL_STATE_INACTIVE`, `GUARDRAIL_STATE_EVALUATE_ONLY`, `GUARDRAIL_STATE_ACTIVE`. If not provided state is set to `GUARDRAIL_STATE_ACTIVE`.

### Read-Only

- `id` (String) UUID for the current version of the guardrail.
- `series_id` (String) UUID for the guardrail.
- `version` (Number) Incrementing version number for the current version of the guardrail.

## Import

A guardrail can be imported using its series_id.

```shell
terraform import resourcely_guardrail.example 00000000-00000000-00000000-00000000
```
